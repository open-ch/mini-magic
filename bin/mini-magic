#!/usr/bin/perl
###############################################################################
#
# mini-magic: CLI tool to help building a minimal magic databases
#
# Copyright (c) 2021 Open Systems AG, Switzerland
# All Rights Reserved.
#
# This tool allows you to take several MIME type definitions and, given a list
# of MIME types, remove all the unnecessary tests. The created magic file can
# then be compiled with the file command. This tool also offers other features such
# as downloading the MIME type definitions from the official repository or list
# all the MIME types for which a test is available.
#
###############################################################################

use v5.28;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/../lib";
use Getopt::Long qw(:config posix_default no_ignore_case);
use MiniMagic;
use Log::Any qw($log);
use Log::Log4perl;
use Log::Log4perl::Level;
use Log::Any::Adapter;
use Cwd qw(abs_path);

# Print command line usage help text
sub exit_with_help {
    print <<~'USAGE';
            Usage:
            mini-magic --mime-types type1,type2,...,typeN [<OPTIONAL PARAMETERS>]
            mini-magic --all [<OPTIONAL PARAMETERS>]
            mini-magic --from-file file [<OPTIONAL PARAMETERS>]
            mini-magic --list [<OPTIONAL PARAMETERS>]
            mini-magic --download [<OPTIONAL PARAMETERS>]
            mini-magic --help

            --mime-types type1,type2,...,typeN    Creates a magic file containing the tests needed to detect the MIME types type1 to typeN.
                                            This magic file is not compiled. To do so, use the file command (with the appropriate version) as follow:
                                            file -C -m /path/to/magic_file
            -a|--all                        Creates a magic file with all available MIME types.
            -f|--from-file file             Create a magic file with the tests corresponding to the list of MIME types in file.
                                            In file, each line correspond to a MIME type.
            -l|--list                       Prints all the MIME types defined by the tests in the source directory for MIME type definitions.
            -d|--download                   Download the MIME type definitions from http://ftp.astron.com/pub/file/ with a given version and save it to the source directory for MIME type definitions.
            -h|--help                       Print this help message.

            OPTIONAL PARAMETERS:

            --debug                         More verbose output (to stdout not syslog).
            --version                       Libmagic version (default 5.39). This optional parameter is only used with --download.
            --src                           MIME type source directory (default Magdir/).
            --magic-filename               Name of the created magic file (default magic). This optional parameter is only used with --mime-types, --all and --from-file.
        USAGE
    exit 1;
}

sub main {
    
    my %args = (
        'mandatory' => {
            'download' => 0,
            'list' => 0,
            'mime types' => '',
            'all' => 0,
            'filename' => '',
        },
        'optional' => {
            'debug' => 0,
            'version' => '',
            'source' => '',
            'magic filename' => '',
        },
    );

    my @mime_types;

    GetOptions(
        'download|d'            => \$args{'mandatory'}->{'download'},
        'list|l'                => \$args{'mandatory'}->{'list'},
        'mime-types=s'          => \$args{'mandatory'}->{'mime types'},
        'all|a'                 => \$args{'mandatory'}->{'all'},
        'from-file|f=s'         => \$args{'mandatory'}->{'filename'},
        "debug"                 => \$args{'optional'}->{'debug'},
        "version=s"             => \$args{'optional'}->{'version'},
        "src=s"                 => \$args{'optional'}->{'source'},
        "magic-filename=s"     => \$args{'optional'}->{'magic filename'},
        "help|h"                => \&exit_with_help, 
    ) or exit_with_help();
    
    check_args(\%args);
    $args{'optional'}->{'version'}          ||= '5.39';
    $args{'optional'}->{'source'}           ||= 'Magdir';
    $args{'optional'}->{'magic filename'}   ||= 'magic';

    configure_log($args{'optional'}->{'debug'});

    if ($args{'mandatory'}->{'download'}) {
        MiniMagic::download_magic_files( abs_path($args{'optional'}->{'source'}), $args{'optional'}->{'version'} );
    }
    if ($args{'mandatory'}->{'filename'}) {
        $args{'mandatory'}->{'filename'} = abs_path($args{'mandatory'}->{'filename'});
        die "$args{'mandatory'}->{'filename'} does not exist" if ( !-f $args{'mandatory'}->{'filename'} );

        open( my $fh, '<', $args{'mandatory'}->{'filename'} );
        while ( my $line = readline($fh)) {
            chomp($line);
            next if !$line;
            push @mime_types, $line;
        }
        close($fh);
    } 
    elsif ( $args{'mandatory'}->{'mime types'}){
        @mime_types = split(',', $args{'mandatory'}->{'mime types'});
    }

    if ($args{'mandatory'}->{'list'}) {
        MiniMagic::print_list_mime_types( abs_path($args{'optional'}->{'source'}) );
    }

    if ($args{'mandatory'}->{'all'}) {
        my $mime_list = MiniMagic::list_mime_types( abs_path($args{'optional'}->{'source'}) );
        MiniMagic::create_mini_magic_file( $mime_list, abs_path($args{'optional'}->{'source'}),
            abs_path($args{'optional'}->{'magic filename'}) );
    }
    elsif (@mime_types) {
        MiniMagic::create_mini_magic_file( \@mime_types, abs_path($args{'optional'}->{'source'}),
            abs_path($args{'optional'}->{'magic filename'}) );
    }
}

sub check_args {
    my $args = shift;
    my $set;

    # Check that one and only one of the mutually exclusive mandatory flags is set
    for my $key (keys %{$args->{'mandatory'}}) {
        my $arg = $args->{'mandatory'}->{$key}; 
        
        if ($arg) {
            exit_with_help() if $set;
            $set = 1;
        }
    }

    # the MIME type list is also mutually exclusive
    exit_with_help() unless $set;

    # Check if the optional arguments are correct
    if ( $args->{'mandatory'}->{'mime types'} || $args->{'mandatory'}->{'all'} || $args->{'mandatory'}->{'filename'} ) {
        exit_with_help() if $args->{'optional'}->{'version'};
    }
    elsif ($args->{'mandatory'}->{'download'}) {
        exit_with_help() if $args->{'optional'}->{'magic filename'};
    }
    elsif ($args->{'mandatory'}->{'list'}) {
        exit_with_help() if $args->{'optional'}->{'version'} || $args->{'optional'}->{'magic filename'};
    }

    return;
}

sub configure_log {
    my ($debug) = @_;
    my $arg_level = $debug ? 'DEBUG' : 'INFO';
    my %conf;
    $conf{'log4perl.logger'} = $arg_level;
    $conf{'log4perl.appender.Screen'} = 'Log::Dispatch::Screen';
    $conf{'log4perl.appender.Screen.layout'} = 'Log::Log4perl::Layout::PatternLayout';
    $conf{'log4perl.appender.Screen.layout.ConversionPattern'} = '%d mini-magic[%P]: [%p] %m%n';
    $conf{'log4perl.appender.Screen.stderr'} = 0;
    $conf{'log4perl.logger'} .= ', Screen';

    # Configure Log4perl and Log::Any
    Log::Log4perl::init( \%conf );
    Log::Any::Adapter->set('Log::Log4perl');
}


main();
